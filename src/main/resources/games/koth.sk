#----------------------------------------------------------------------------
options:
#basic
    var_prefix: game::koth

#----------------------------------------------------------------------------

on "koth" game start:
    set {_spawnpoints::*} to value "spawnpoints" of event-session
    loop (players of event-session):
        add 1 to {_n}
        teleport loop-player to {_spawnpoints::%{_n}%}
        set slot 0 of loop-player to stick
        reset {_n} if {_n} is (size of {_spawnpoints::*})
    Koth_StartHillTicking(event-session)
    Koth_StartShowPoints(event-session)

on "koth" game stop:
    if event-reason is "WIN":
        set {_winner} to {_additional_info}
        add "&7Player &f%{_winner}% &7won the game" to {_info::*}
        GE_Stopped(event-session, event-reason, {_info::*})
    else:
        GE_Stopped(event-session, event-reason)

local function Koth_StartShowPoints(session: session):
    while (state of {_session}) is started:
        loop (players of {_session}):
            set {_s} to session value "players::%loop-value%::score" of {_session}
            send action bar "&7Points: %{_s} ? 0%" to loop-player
        wait 1 second

local function Koth_StartHillTicking(session: session):
    set {_map} to map of {_session}
    set {_bound} to value "hill_bound" of {_map} and (gamemode of {_session})
    while (state of {_session}) is started:
        set {_p::*} to (bound players of {_bound})
        if (size of {_p::*}) = 1:
            set {_s} to value "players::%{_p::1}%::score" of {_session}
            set value "players::%{_p::1}%::score" of {_session} to {_s} + 1
            if ({_s} + 1) >= GE_GetSessionOption({_session},"points_limit"):
                set value "WINNER" of {_session} to {_p::1}
                stop game of {_session} with reason "WIN"
        wait 1 second

on damage of player:
    # Internally checks if player is in session, the session is running, the session is started, gamemode of the session is "koth
    victim is playing (gamemode with id "koth")
    cancel event
    attacker is a player
    set {_sp} to 1
    if (tool of attacker) is stick:
        add 0.5 to {_sp}
    if (attacker is sprinting):
        add 0.5 to {_sp}
    push victim (vector from attacker to victim) with speed {_sp}
    push victim up with speed 0.5

on drop:
    player is playing (gamemode with id "koth")
    cancel event