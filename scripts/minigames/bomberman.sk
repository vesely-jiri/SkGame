#----------------------------------------------------------------------------
options:
#basic
    var_prefix: game::bomberman
    common_radius: 5
    rare_radius: 5
    legendary_radius: 7
    uncommon_range: 10
    legendary_range: 15
    startup_time: 5
#messages
    player_eliminated: "&cPlayer %{_victim}% has been eliminated by %{_attacker}%"
    shield_destroyed: "&cYour shield has been destroyed!"

#----------------------------------------------------------------------

#TODO:
#   remake mines to variables(or something else?), so it's easier to remove references. Rn metadata are being just stacked in memory until server is restarted

on load:
    register new minigame with id "bomberman":
        set minigame value "name" of event-minigame to "&cBomberMan"
        set minigame value "icon" of event-minigame to TNT
        set minigame value "description" of event-minigame to "&7- Pickup your &cTNT &7and fight"
        set minigame value "author" of event-minigame to "JuraJ_Player"
        set minigame value "min_players" of event-minigame to 2
        set minigame value "session;gamemode" of event-minigame to a custom value:
            set value name to "Gamemodes"
            set value type to a text
            set default value to "elimination"
            set description to "Choose which gamemode you want to play. Elimination with just one life, Score deathmatch or time limited deathmatch"
        set minigame value "map;respawn_cooldown" of event-minigame to a custom value:
            set plurality to single
            set value name to "Respawn cooldown"
            set value type to a timespan
            set default value to 5 seconds
            set description to "Choose how long it will take player to respawn after death in deathmatch mode"
        set minigame value "map;spawn_rate" of event-minigame to a custom value:
            set value name to "Booster spawnpoints"
            set plurality to single
            set value type to a timespan
            set default value to 5 ticks
            set description to "Choose how fast are items going to spawn"
        set minigame value "map;deathmatch_timer" of event-minigame to a custom value:
            set value name to "Deathmatch playtime"
            set plurality to single
            set value type to a timespan
            set default value to 1 minute
            set description to "Choose how long will players play, if deathmatch mode is set"
        set minigame value "map;booster_spawnpoints" of event-minigame to a custom value:
            set value name to "Booster spawnpoints"
            set plurality to plural
            set value type to a location
            set description to "Locations on map where items are going to be spawned. Players are also spawned on those locations"

on "bomberman" game start:
    set {_loc::*} to pair values "map;booster_spawnpoints" of event-gamemap of event-minigame
    #TODO: teleport (spectators of event-session) to bound center of (arena of event-session)
    loop shuffled (players of event-session):
        add 1 to {_n}
        teleport loop-value to {_loc::%{_n}%}
        set gamemode of loop-value to survival
        reset {_n} if {_n} is size of {_loc::*}
    loop reversed {@startup_time} times:
        set level of (players of event-session) to loop-value - 1
        if loop-value <= 3:
            play sound "minecraft:entity.experience_orb.pickup" to players of event-session
        wait 1 second
    set {_rate} to pair value "map;spawn_rate" of event-gamemap of event-minigame

    Bomberman_StartSpawningDrops(event-session,{_rate})
    if Bomberman_GetGamemode(event-session) is "deathmatch":
        Bomberman_StartTimer(event-session)

on "bomberman" game stop:
    #set {_arena} to arena of (map of {_session})
    delete bound (primed tnt) in {_arena}
    delete bound (dropped items) in {_arena}
    set {_mine} to {{@var_prefix}::items::mine}
    #TODO: refactor: Probably lagging
    set (bound blocks within {_bound}) where [input is type of {_mine}] to air
    set {_p::*} to Bomberman_GetPlayingPlayers(event-session)
    if event-string is "WIN":
        set {_m} to "&aWinner is %{_p::1}%"
    else if event-string is "TIMEOUT":
        loop {_p::*}:
            set {_v} to temporary player value "points" of loop-value
            add loop-value to {_player-list::*}
            set {_players::%loop-value%} to {_v} ? 0
            broadcast "&5Found value: %{_players::%loop-value%}% for player %loop-value%"
        sort {_player-list::*} in descending order based on {_players::%input%}
        set {_kills} to {_players::%{_player-list::1}%}
        set {_m} to "&aWinner is: %{_player-list::1}% with %{_kills}% kills"
        set {_win} to "WIN"
    broadcast "&cStop reason is: %event-string%"
    G_Stopped(event-session, {_win} ? event-string, ({_m} ? ""))

#----------------------------------------------------------------------

on script load:
    delete {{@var_prefix}::probabilities::*}
    set {{@var_prefix}::items::extra_life} to netherite chestplate named "&cExtra life"
    set {{@var_prefix}::items::extra_life::chance} to 5
    set {{@var_prefix}::items::mine} to polished blackstone pressure plate named "&cMine"
    set {{@var_prefix}::items::mine::chance} to 10
    set {{@var_prefix}::items::speed_boots} to 1 of netherite boots named "&5Speedos"
    set {{@var_prefix}::items::speed_boots::chance} to 10
    set {{@var_prefix}::items::common_tnt} to 1 of tnt named "&2Common TNT"
    set {{@var_prefix}::items::common_tnt::chance} to 30
    set {{@var_prefix}::items::common_tnt::fuse} to 60
    set {{@var_prefix}::items::uncommon_tnt} to 1 of tnt named "&aUncommon TNT"
    set {{@var_prefix}::items::uncommon_tnt::chance} to 25
    set {{@var_prefix}::items::uncommon_tnt::fuse} to 40
    set {{@var_prefix}::items::rare_tnt} to 1 of tnt named "&5Rare TNT"
    set {{@var_prefix}::items::rare_tnt::chance} to 15
    set {{@var_prefix}::items::rare_tnt::fuse} to 40
    set {{@var_prefix}::items::legendary_tnt} to 1 of tnt named "&cLegendary TNT"
    set {{@var_prefix}::items::legendary_tnt::chance} to 5
    set {{@var_prefix}::items::legendary_tnt::fuse} to 30
    # Find lowest chance that will be used to divide all chances(minimize storage usage)
    loop {{@var_prefix}::items::*}:
        set {_v} to {{@var_prefix}::items::%loop-index%::chance}
        set {_s} to {_v} if ({_s} ? infinity value) > {_v}
    # Add the item as many times as their chance/lowest chance, then keep picking random items from list
    loop {{@var_prefix}::items::*}:
        loop ceil({{@var_prefix}::items::%loop-index%::chance}/{_s}) times:
            add loop-index to {{@var_prefix}::probabilities::*}

#----------------------------------------------------------------------

local function Bomberman_GetGamemode(session: session) :: string:
    set {_g} to temporary session value "gamemode" of {_session}
    return "deathmatch" if {_g} is 1
    return "elimination"

local function Bomberman_StartTimer(session: session):
    set {_mg} to minigame of {_session}
    set {_gm} to gamemap of {_session}
    set {_timer} to (pair value "map;deathmatch_timer" of {_gm} of {_mg})
    while (state of {_session}) is STARTED:
        if {_timer} <= (loop-iteration * 1 second):
            stop game of {_session} with reason "TIMEOUT"
        wait 1 second

local function Bomberman_GetPlayingPlayers(session: session) :: players:
    return (players of {_session}) where [gamemode of input is survival]

local function Bomberman_GetFuseTime(type: string) :: integer:
    return {{@var_prefix}::items::%{_type}%::fuse}

local function Bomberman_GetRandomItemIndex() :: string:
    return random element of {{@var_prefix}::probabilities::*}

local function Bomberman_RespawnPlayer(session: session, p: player):
    set {_cd} to pair value "map;respawn_cooldown" of (gamemap of {_session}) of (minigame of {_session})
    wait {_cd}
    if {_p} is playing (minigame with id "bomberman"):
        set (gamemode of {_p}) to survival
        set {_map} to map of {_session}
        set {_mg} to minigame of {_session}
        set {_l::*} to pair values "map;booster_spawnpoints" of {_map} of {_mg}
        teleport {_p} to (random element of {_l::*})
        set gamemode of {_p} to survival

local function Bomberman_AddPoint(session: session, p: player):
    play sound "minecraft:entity.experience_orb.pickup" to {_p}
    set {_v} to temporary player value "points" of {_p}
    set temporary player value "points" of {_p} to ({_v} + 1)
    set (level of {_p}) to ({_v} + 1)

local function Bomberman_SpawnDrops(mode: string, l: locations):
    if {_mode} is "charge":
        loop {_l::*}:
            set {_index} to Bomberman_GetRandomItemIndex()
            set {_i} to {{@var_prefix}::items::%{_index}%}
            if {_i} is a tnt:
                set string tag "{@var_prefix};type" of (custom nbt of {_i}) to {_index}
            drop {_i} at loop-value without velocity
    else if {_mode} is "random":
        set {_l} to random element of {_l::*}
        set {_index} to Bomberman_GetRandomItemIndex()
        set {_i} to {{@var_prefix}::items::%{_index}%}
        if {_i} is a tnt:
            set string tag "{@var_prefix};type" of (custom nbt of {_i}) to {_index}
        drop {_i} at {_l} without velocity

local function Bomberman_StartSpawningDrops(session: session, rate: timespan):
    set {_map} to map of {_session}
    set {_mg} to minigame of {_session}
    set {_l::*} to pair values "map;booster_spawnpoints" of {_map} of {_mg}
    Bomberman_SpawnDrops("charge",{_l::*})
    while state of {_session} is STARTED:
        Bomberman_SpawnDrops("random",{_l::*})
        wait {_rate}

local function Bomberman_EliminatePlayer(victim: player, attacker: player):
    set {_session} to session of {_victim}
    if invulnerability time of {_victim} is not 0 ticks:
        stop
    set invulnerability time of {_victim} to 2 ticks
    if chestplate of {_victim} is air:
        set (gamemode of {_victim}) to spectator
        set {_v} to temporary player value "deaths" of {_victim}
        set temporary player value "deaths" of {_victim} to {_v} + 1
        play sound "minecraft:entity.player.hurt" at {_victim}
        G_SendMessageToSession({_session},{@player_eliminated})
        if {_victim} is not {_attacker}:
            Bomberman_AddPoint({_session},{_attacker})
        if Bomberman_GetGamemode({_session}) is "elimination":
            if size of Bomberman_GetPlayingPlayers({_session}) is 1:
                stop game of {_session} with reason "WIN"
            else if size of Bomberman_GetPlayingPlayers({_session}) is 0:
                stop game of {_session} with reason "NO_PLAYERS"
        else if Bomberman_GetGamemode({_session}) is "deathmatch":
            Bomberman_RespawnPlayer({_session},{_victim})
    else:
        send action bar {@shield_destroyed} to {_victim}
        play sound "minecraft:item.shield.break" with pitch 1 at {_victim}
        set chestplate of {_victim} to air
        
local function Bomberman_CommonExplode(l: location, attacker: player, radius: integer = {@common_radius}):
    loop (blocks in radius {_radius} of {_l}) where [input is not air]:
        add loop-block to {_blocks::*}
    show 2 explosion at {_blocks::*}
    show 2 poof at {_blocks::*}
    loop players in radius {_radius} of {_l}:
        gamemode of loop-player is not spectator
        Bomberman_EliminatePlayer(loop-player,{_attacker})

local function Bomberman_UncommonExplode(l: location, attacker: player, range: integer = {@uncommon_range}):
    loop vector({_range},0,0) and vector(0,0,{_range}) and vector({_range}*-1,0,0) and vector(0,0,{_range}*-1):
        loop blocks from {_l} to ({_l} ~ loop-value):
            if loop-block is air:
                add loop-block to {_blocks::*}
                add (players in radius 1 of loop-block) to {_players::*}
            else:
                stop loop
    show 2 explosion at {_blocks::*}
    show 2 poof at {_blocks::*}
    loop {_players::*}:
        Bomberman_EliminatePlayer(loop-value,{_attacker})

local function Bomberman_RareExplode(l: location, attacker: player):
    Bomberman_CommonExplode({_l},{_attacker},{@rare_radius})

local function Bomberman_LegendaryExplode(l: location, attacker: player):
    Bomberman_CommonExplode({_l},{_attacker},{@legendary_radius})
    Bomberman_UncommonExplode({_l},{_attacker},{@legendary_range})

local function Bomberman_ExecuteAbility(e: entity):
    set {_t} to metadata tag "{@var_prefix};type" of {_e}
    set {_a} to metadata tag "{@var_prefix};owner" of {_e}
    create fake explosion at {_e}
    play sound "minecraft:entity.generic.explode" at {_e}
    set {_l} to location of (block at {_e})
    if {_t} is "common_tnt":
        Bomberman_CommonExplode({_l},{_a})
    else if {_t} is "uncommon_tnt":
        Bomberman_UncommonExplode({_l},{_a})
    else if {_t} is "rare_tnt":
        Bomberman_RareExplode({_l},{_a})
    else if {_t} is "legendary_tnt":
        Bomberman_LegendaryExplode({_l},{_a})

on explode:
    event-entity is a tnt
    metadata tag "{@var_prefix};type" of event-entity is set
    cancel event
    Bomberman_ExecuteAbility(event-entity)

on rightclick with tnt:
    player is playing (minigame with id "bomberman")
    set {_t} to string tag "{@var_prefix};type" of (custom nbt of event-item)
    if {_t} is set:
        cancel event
        make player swing hand
        if {_t} is "rare_tnt" or "legendary_tnt":
            shoot (primed tnt) from player with speed 2
            set {_e} to last shot primed tnt
            remove 1 of (tool of player) from (tool of player)
        else:
            event-block is set
            block at (event-block ~ vector(0,1,0)) is air
            spawn (primed tnt) at event-block ~ vector(0,1,0)
            set {_e} to last spawned primed tnt
            remove 1 of (tool of player) from (tool of player)
        set short tag "fuse" of (nbt of {_e}) to Bomberman_GetFuseTime({_t})
        set metadata tag "{@var_prefix};type" of {_e} to {_t}
        set metadata tag "{@var_prefix};owner" of {_e} to player

on damage of player:
    victim is playing (minigame with id "bomberman")
    cancel event

on pickup:
    player is playing (minigame with id "bomberman")
    event-item is {{@var_prefix}::items::speed_boots}
    if (items of inventory of player) contains event-item:
        cancel event
        delete event-dropped item

on armor change:
    player is playing (minigame with id "bomberman")
    new armor item is {{@var_prefix}::items::speed_boots}
    while (boots of player) is netherite boots:
        player is playing (minigame with id "bomberman")
        apply speed without particles without the icon to player for 2 second replacing existing effect
        wait 1 second

on place:
    player is playing (minigame with id "bomberman")
    1 of (tool of player) is {{@var_prefix}::items::mine}
    set metadata tag "{@var_prefix};attacker" of event-block to player

on pressure plate:
    player is playing (minigame with id "bomberman")
    set {_a} to metadata tag "{@var_prefix};attacker" of event-block
    if {_a} is set:
        create fake explosion at event-block
        play sound "minecraft:entity.generic.explode" at event-block
        cancel event
        set (block at event-location) to air
        push player (vector from event-block to player) at speed 1
        Bomberman_EliminatePlayer(player,{_a})